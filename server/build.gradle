plugins {
    id "java"
    id "groovy"
    id "io.micronaut.application" version "3.5.1"
    id "com.github.ben-manes.versions" version "0.42.0"
}

group "org.exacaster.lighter"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

micronaut {
    version = "3.5.1"
    runtime "netty"
}
mainClassName = "com.exacaster.lighter.Application"

def sparkVersion = project.hasProperty("SPARK_VERSION") ? project.getProperty("SPARK_VERSION") : '3.3.0'
def hadoopVersion = '3.3.3'
def k8sClientVersion = '6.0.0'
def shedlockVersion = '4.38.0'

dependencies {
    annotationProcessor "io.micronaut:micronaut-validation"
    annotationProcessor "io.micronaut.openapi:micronaut-openapi"

    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut.flyway:micronaut-flyway"
    implementation "io.swagger.core.v3:swagger-annotations"

    implementation "io.micronaut.sql:micronaut-jdbc-hikari"
    implementation "io.micronaut.sql:micronaut-jdbi"
    implementation "io.micronaut.data:micronaut-data-processor"
    implementation "io.micronaut:micronaut-http-client"

    implementation "org.apache.spark:spark-launcher_2.12:${sparkVersion}"
    implementation "io.fabric8:kubernetes-client:${k8sClientVersion}"
    implementation "org.apache.hadoop:hadoop-yarn-client:${hadoopVersion}"
    implementation "org.apache.hadoop:hadoop-common:${hadoopVersion}"
    implementation 'net.sf.py4j:py4j:0.10.9.5'

    implementation "net.javacrumbs.shedlock:shedlock-micronaut:${shedlockVersion}"
    implementation "net.javacrumbs.shedlock:shedlock-provider-jdbc-micronaut:${shedlockVersion}"

    runtimeOnly "io.micronaut.micrometer:micronaut-micrometer-core"
    runtimeOnly "io.micronaut.micrometer:micronaut-micrometer-registry-prometheus"
    runtimeOnly "io.micronaut:micronaut-management"

    runtimeOnly "com.h2database:h2"
    runtimeOnly "org.postgresql:postgresql"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.0"
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation "io.fabric8:kubernetes-server-mock:${k8sClientVersion}"
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.forkOptions.jvmArgs << '-Dmicronaut.openapi.views.spec=swagger-ui.enabled=true,swagger-ui.theme=flattop'
}

test {
    useJUnitPlatform()
}
